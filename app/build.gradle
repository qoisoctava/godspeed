plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs.kotlin' version '2.7.7'
    id 'kotlin-kapt'
}

android {
    namespace 'com.viliussutkus89.iamspeed'
    compileSdk 35
    defaultConfig {
        applicationId "com.viliussutkus89.iamspeed"
        minSdk 14
        targetSdk 34

        versionCode 291
        versionName "1.2.4"
        if (project.hasProperty('test_build')) {
            versionNameSuffix = ".unreleased"
            applicationIdSuffix = ".unreleased"
        }

        vectorDrawables.useSupportLibrary true

        multiDexEnabled true
        // https://github.com/android/android-test/issues/1433
        // https://issuetracker.google.com/issues/78108767#comment12
        multiDexKeepProguard file('multidex.pro')

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    testOptions.execution 'ANDROIDX_TEST_ORCHESTRATOR'

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix = ".debug"
            applicationIdSuffix = ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    lint.abortOnError = false

    if (System.getenv('SIGNING_KEYFILE') && System.getenv('SIGNING_ALIAS') && System.getenv('SIGNING_PASS')) {
        signingConfigs {
            release {
                storeFile file(System.getenv('SIGNING_KEYFILE'))
                storePassword System.getenv('SIGNING_PASS')
                keyAlias System.getenv('SIGNING_ALIAS')
                keyPassword System.getenv('SIGNING_PASS')
            }
        }
        buildTypes.release.signingConfig signingConfigs.release
    }
}

dependencies {
    //noinspection GradleDependency androidx.core:core-ktx:1.13.0 requires minSdk 19
    implementation 'androidx.core:core-ktx:1.12.0'

    implementation 'androidx.multidex:multidex:2.0.1'

    //noinspection GradleDependency androidx.appcompat:appcompat:1.7.0 requires minSdk 21
    implementation 'androidx.appcompat:appcompat:1.6.1'

    //noinspection GradleDependency com.google.android.material:material:1.12.0 requires minSdk 19
    implementation 'com.google.android.material:material:1.11.0'

    //noinspection GradleDependency androidx.lifecycle:lifecycle requires minSdk 19
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.2'
    //noinspection GradleDependency
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    //noinspection GradleDependency
    implementation 'androidx.lifecycle:lifecycle-service:2.6.2'

    implementation 'androidx.preference:preference:1.2.1'

    implementation "androidx.navigation:navigation-fragment-ktx:2.7.7"
    implementation "androidx.navigation:navigation-ui-ktx:2.7.7"

    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.3"

//  androidx.test:core 1.4.0 sometimes take extra 45 seconds
//    androidTestImplementation "androidx.test:core:1.4.0"
    androidTestImplementation "androidx.test:core:1.5.0-rc01"

    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestUtil 'androidx.test:orchestrator:1.4.1'
    androidTestImplementation "androidx.test:rules:1.4.0"

    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.4.0"
    implementation "androidx.test.espresso:espresso-idling-resource:3.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.4.0"

    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}
